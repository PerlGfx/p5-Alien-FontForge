use alienfile;

plugin 'PkgConfig' => 'libfontforge';

requires 'Alien::Build::Plugin::Gather::Dino';
requires 'Alien::gmake';
requires 'Path::Tiny' => 0;
if ( exists $ENV{MSYSTEM} ) {
	requires 'Archive::Zip'; # for Extract::ArchiveZip
}

# https://fontforge.github.io/
share {
	# Use .zip on Windows' MSYS2 because of extraction issues with bsdtar under MSYS2
	my $ext = exists $ENV{MSYSTEM} ? 'zip' : 'tar.gz';
	plugin Download => (
		url => 'https://github.com/fontforge/fontforge/releases',
		version => qr/(\d{4}\d{2}\d{2})\.\Q$ext\E/,
	);

	if ( exists $ENV{MSYSTEM} ) {
		plugin 'Extract::ArchiveZip' => $ext;
	} else {
		plugin 'Extract::CommandLine' => $ext;
	}

	patch sub {
		my($splinefont_h) = Path::Tiny->new('fontforge/splinefont.h');

		if( $^O eq 'darwin' ) {
			$splinefont_h->edit_lines(sub {
				s{\Q#include "locale.h"\E}{#include <locale.h>\n#include <xlocale.h>\n}g;
			});
		}
	};

	build [
		[ 'cmake', qw(-G), 'Unix Makefiles',
			'-DCMAKE_INSTALL_PREFIX:PATH=%{.install.prefix}',
			'-DENABLE_GUI=OFF',
			'-DENABLE_PYTHON_SCRIPTING=OFF',
			'-DENABLE_PYTHON_EXTENSION=OFF',
			'-DENABLE_DOCS=OFF',
			'-S', '.',
			'-B', 'build',
			],
		[ '%{gmake}', '-C', 'build' ],
		[ '%{gmake}', '-C', 'build', 'install' ],
	];

	plugin 'Gather::Dino';
};
